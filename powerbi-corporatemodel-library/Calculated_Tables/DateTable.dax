-- DateTable with Fiscal and Calendar breakdowns
-- Author: Jordan Taylor

DateTable = 
VAR MinDate = MIN(FactTable[Date])
VAR MaxInputDate = MAX(FactTable[Date])
VAR MaxDate = DATE(YEAR(MaxInputDate), 12, 31) //Extends DateTable to the end of the year

RETURN
ADDCOLUMNS(
    CALENDAR(MinDate, MaxDate),
    
    -- Standard Date Parts
    "Year", YEAR([Date]),
    "Month", MONTH([Date]),
    "Month Name", FORMAT([Date], "MMMM"),
    "Month Short", FORMAT([Date], "MMM"),
    "Quarter", "Q" & QUARTER([Date]),
    "YearMonth", FORMAT([Date], "MMM'yy"),
    "YearQuarter", "Q" & QUARTER([Date]) & "'" & FORMAT([Date], "yy"),

    -- Fiscal Date Parts (Fiscal Year starts July)
    "Fiscal Year", IF(MONTH([Date]) >= 7, YEAR([Date]) + 1, YEAR([Date])),
    "Fiscal Period", IF(MONTH([Date]) >= 7, MONTH([Date]) - 6, MONTH([Date]) + 6),
    "Fiscal Quarter", 
        "Q" & 
        SWITCH(TRUE(),
            MONTH([Date]) IN {7,8,9}, 1,
            MONTH([Date]) IN {10,11,12}, 2,
            MONTH([Date]) IN {1,2,3}, 3,
            MONTH([Date]) IN {4,5,6}, 4
        ),

    -- Extras
    "Month Number in Year", MONTH([Date]),
    "Month Number in Fiscal Year", IF(MONTH([Date]) >=7, MONTH([Date])-6, MONTH([Date])+6),
    "IsToday", IF([Date] = TODAY(), TRUE(), FALSE()),
    "StartOfMonth", DATE(YEAR([Date]), MONTH([Date]), 1),
    "EndOfMonth", EOMONTH([Date], 0),

    -- Start and End of Quarter
    "StartOfQuarter", DATE(YEAR([Date]), (QUARTER([Date]) - 1) * 3 + 1, 1),
    "EndOfQuarter", EOMONTH(DATE(YEAR([Date]), (QUARTER([Date]) - 1) * 3 + 3, 1), 0)
)
